#version 460 core

// inputs
layout(rgba32f, binding = 0) uniform image2D screen;

uniform vec3 cam_pos;
uniform vec2 plane_dims;
uniform float near_plane;
uniform float far_plane;

uniform mat4 inverse_view;
uniform mat4 inverse_projection;

struct Material {
	vec4 colour;
};

struct Sphere {
	vec3 pos;
	float rad;

	Material material;
};

layout(std430, binding = 1) buffer spheres_buffer {
	Sphere spheres[];
};
uniform int spheres_max;

struct Ray {
    vec3 origin;
    vec3 dir;
};

struct HitRecord {
	bool hit;
	vec3 point;
	vec3 normal;
	float dist;
	bool front_face;
};

#define set_face_normal(h, r) if (dot(r.dir, h.normal) > 0.0) { \
								h.normal = -h.normal; \
								h.front_face = false; \
							  } else { \
								h.front_face = true; \
							  }

Ray createRay(vec3 origin, vec3 dir) { 
	Ray new_ray;
	new_ray.origin = origin;
	new_ray.dir = dir;
	return new_ray;
}

vec4 intersectWorld(Ray ray);
HitRecord intersectSphere(int sIndex, Ray r);


layout(local_size_x = 8, local_size_y = 8, local_size_z = 1) in;
void main() {
	ivec2 output_dims = imageSize(screen);
	ivec2 p = ivec2(gl_GlobalInvocationID.xy);
	
	if (p.x > output_dims.x || p.y > output_dims.y) {
		return;
	}

	vec4 pixel = vec4(0.075, 0.133, 0.173, 1.0);
	
	vec2 uv = vec2(0, 0);
    uv.x = float((-float(p.x * 2.0) / output_dims.x) + 1.0); // transform to [-1.0, 1.0]
    uv.y = -float((-float(p.y * 2.0) / output_dims.y) + 1.0); // transform to [-1.0, 1.0]
	
	vec4 target = inverse_projection * vec4(uv, 1.0, 1.0);
	vec3 ray_dir = vec3(inverse_view * vec4(normalize(target.xyz / target.w), 0));

	Ray ray = createRay(cam_pos, normalize(ray_dir));

	vec4 result = intersectWorld(ray);

	imageStore(screen, p, result);
}

vec4 intersectWorld(Ray ray) {
	vec4 colour = vec4(0.0, 0.0, 0.0, 1.0);

	HitRecord closestRecord;
	closestRecord.dist = far_plane + 1.0;
	HitRecord currentRecord;

	// check for intersections with spheres
	for (int i = 0; i < spheres_max; i++) {
		currentRecord = intersectSphere(i, ray);
		if (currentRecord.hit) {
			if (currentRecord.dist < closestRecord.dist) {
				closestRecord = currentRecord;	
				colour = spheres[i].material.colour;
			}
		}
	}

	vec3 light_dir = normalize(vec3(-1, -1, -1));
	colour = vec4(colour.xyz * max(dot(closestRecord.normal, -light_dir), 0.0), 1.0);

	return colour;
}

HitRecord intersectSphere(int sIndex, Ray r) {
	Sphere s = spheres[sIndex];

	vec3 o = r.origin - s.pos;
	float a = dot(r.dir, r.dir);
	float half_b = dot(o, r.dir);
	float c = dot(o, o) - (s.rad * s.rad);
	float discriminant = (half_b * half_b) - (a * c);

	HitRecord hit;
	if (discriminant < 0) {
		hit.hit = false;
		return hit;
	}
	float sqrtd = sqrt(discriminant);
	float root = (-half_b - sqrtd) / a;
	if (root < near_plane || root > far_plane) {
		root = (-half_b + sqrtd) / a;
		if (root < near_plane || root > far_plane) {
			hit.hit = false;
			return hit;
		}
	}
	hit.hit = true;
	hit.dist = root;
	hit.point = r.origin + (root * r.dir);
	hit.normal = (hit.point - s.pos) / s.rad;
	set_face_normal(hit, r);
	return hit;
}
